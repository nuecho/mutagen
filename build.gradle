plugins {
  id "com.diffplug.gradle.spotless" version "3.8.0"
}

repositories {
  mavenCentral()
  maven { url 'http://knox.s.nuecho.com/artifactory/repo' }
}

apply plugin: 'application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'com.diffplug.gradle.spotless'

description = 'Nu Echo\'s Genesys Command Line Interface'
group = 'com.nuecho'
version = "${version}"
mainClassName = 'com.nuecho.genesys.cli.GenesysCli'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
  checkstyleConfig
}

checkstyle {
  toolVersion = 8.7
  /*
    Using a non standard config file is required because of this bug:
    https://github.com/google/google-java-format/issues/159

    When fixed, use default Google checks following this method:
    https://discuss.gradle.org/t/checkstyle-how-to-use-an-official-style-configuration/6952/6
  */
  configFile = file('google_checks.xml')
  maxWarnings = 0
}

findbugs {
  effort = "max"
  reportLevel = "low"
}

jacocoTestReport {
  reports {
    html.enabled true
  }
}

spotless {
  java {
    googleJavaFormat()
  }
}

dependencies {
  compile 'info.picocli:picocli:2.2.+'
  compile 'com.google.guava:guava:23.6-jre'
  compile 'com.googlecode.java-diff-utils:diffutils:1.2.+'

  compile 'com.genesyslab.platform:comappblock:+'
  compile 'com.genesyslab.platform:commons:+'
  compile 'com.genesyslab.platform:configurationprotocol:+'

  testCompile 'junit:junit:4.+'
}

run {
  // Allow passing arguments using gradlew run
  // gradlew run -Dexec.args="..."
  args System.getProperty("exec.args", "").split()
}